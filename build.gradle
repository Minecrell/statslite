plugins {
    id 'net.minecrell.licenser' version '0.3'
}

allprojects {
    group = 'net.minecrell.mcstats'
    version = '0.2.3-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    sourceCompatibility = '1.7'
    targetCompatibility = '1.7'

    configurations { compileOnly }

    sourceSets.main.compileClasspath += configurations.compileOnly
    eclipse.classpath.plusConfigurations += [configurations.compileOnly]
    idea.module.scopes.COMPILE.plus += [configurations.compileOnly]
    javadoc.classpath = sourceSets.main.compileClasspath

    repositories {
        mavenCentral()
    }

    task sourceJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: 'javadoc') {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    apply plugin: 'maven-publish'

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                artifact sourceJar
                artifact javadocJar

                repositories {
                    maven {
                        url = System.getenv('REPO_' + (version.endsWith('-SNAPSHOT') ? 'SNAPSHOTS' : 'RELEASES')) ?: "$rootProject.buildDir/repo"
                    }
                }
            }
        }
    }

    apply plugin: 'net.minecrell.licenser'

    license {
        header = rootProject.file('LICENSE')
        include '**/*.java'
    }
}

subprojects {
    dependencies {
        compileOnly rootProject
    }

    jar {
        from rootProject.sourceSets.main.output
    }

    sourceJar {
        from rootProject.sourceSets.main.allSource
    }

    javadoc {
        source rootProject.sourceSets.main.java
    }
}

dependencies {
    compileOnly 'com.google.code.gson:gson:2.2.4'
}

