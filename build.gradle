plugins {
    id 'com.github.hierynomus.license' version '0.11.0'
}

allprojects {
    group = 'net.minecrell.statslite'
    version = '0.2-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: 'eclipse'
    apply plugin: 'idea'

    sourceCompatibility = '1.7'
    targetCompatibility = '1.7'

    configurations {
        provided
    }

    sourceSets {
        main {
            compileClasspath += configurations.provided
        }
    }

    eclipse {
        classpath {
            plusConfigurations += [configurations.provided]
        }
    }

    idea {
        module {
            scopes.PROVIDED.plus += [configurations.provided]
        }
    }

    javadoc.classpath = sourceSets.main.compileClasspath

    repositories {
        mavenCentral()
    }

    task sourceJar(type: Jar) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: 'javadoc') {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    apply plugin: 'maven-publish'

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from components.java

                artifact sourceJar
                artifact javadocJar

                repositories {
                    maven {
                        url = System.getenv('REPO_' + (version.endsWith('-SNAPSHOT') ? 'SNAPSHOTS' : 'RELEASES')) ?: "$rootProject.buildDir/repo"
                    }
                }
            }
        }
    }

    apply plugin: 'com.github.hierynomus.license'

    license {
        header rootProject.file('LICENSE')
        include '**/*.java'

        strictCheck true
        mapping {
            java = 'SLASHSTAR_STYLE'
        }
    }
}

subprojects {
    dependencies {
        provided rootProject
    }

    jar {
        from rootProject.sourceSets.main.output
    }

    sourceJar {
        from rootProject.sourceSets.main.allSource
    }

    javadoc {
        source rootProject.sourceSets.main.java
    }
}

dependencies {
    provided 'com.google.code.gson:gson:2.2.4'
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.10'
}
